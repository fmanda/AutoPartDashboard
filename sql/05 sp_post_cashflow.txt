CREATE PROCEDURE SP_POST_CASHFLOW(
	@MONTHPERIOD INT,
	@YEARPERIOD INT
)
AS 
BEGIN
	DECLARE @BASEURL VARCHAR(100) = (SELECT DISTINCT VARVALUE FROM TVARIABLE WHERE VARNAME = 'REST_URL')	

	DECLARE @PROJECTCODE VARCHAR(100) = (SELECT DISTINCT VARVALUE FROM TVARIABLE WHERE VARNAME = 'KODE_CABANG')
	DECLARE @STARTDATE DATE = DATEFROMPARTS (@YEARPERIOD, @MONTHPERIOD, 1);
	DECLARE @ENDDATE DATE = EOMONTH(@STARTDATE);
	DECLARE @URL VARCHAR(300);

	DECLARE @status int
	DECLARE @req AS INT;
	DECLARE @ResponseText as table(responseText nvarchar(max))
	declare @msg varchar(max) 


	DECLARE @Body AS VARCHAR(MAX) = 
		(			
			select @PROJECTCODE as projectcode, @MONTHPERIOD as monthperiod, @YEARPERIOD as yearperiod,
			sum(purchase) as purchase, sum(sales) as sales, sum(otherincome) as otherincome, sum(otherexpense) as otherexpense
			from (
				SELECT 
				CASE WHEN (HEADER_FLAG = 100) then sum(creditamt)  else 0 end as purchase,
				CASE WHEN (HEADER_FLAG = 200) then sum(debetamt) else 0 end as sales,
				CASE WHEN (HEADER_FLAG = 400) then sum(debetamt) else 0 end as otherincome,
				CASE WHEN (HEADER_FLAG = 500) then sum(creditamt) else 0 end as otherexpense
				FROM TFINANCIALTRANSACTION
				WHERE TRANSDATE BETWEEN @STARTDATE AND @ENDDATE
				AND REKENING_ID IS NOT NULL
				GROUP BY HEADER_FLAG
			) as r
			FOR JSON PATH
		)


	EXEC sp_OACreate 'MSXML2.XMLHTTP', @req OUT;

	-- delete first
	SET @URL = @BASEURL + 'cashflow/' + @PROJECTCODE + '/' 
				+ CAST(@YEARPERIOD AS varchar(10)) + '/'
				+ CAST(@MONTHPERIOD AS varchar(10));
	EXEC sp_OAMethod @req, 'open', NULL, 'DELETE', @URL, 'false'
	EXEC sp_OAMethod @req, 'setRequestHeader', null, 'Content-Type', 'text/html'
	EXEC sp_OAMethod @req, 'send';
	EXEC sp_OAGetProperty @req, 'status', @status OUT
	if (@status <> 200) 
	begin
		INSERT INTO @ResponseText (ResponseText) EXEC sp_OAGetProperty @req, 'responseText'
		set @msg  = @URL + ' => Error Code ' +  cast(@status as varchar(10))
			+ CHAR(10)+ (select distinct isnull(responsetext,'') from @ResponseText);
		THROW 51000, @msg, 1;  
	end

	-- post after
	SET @URL = @BASEURL + 'cashflow'; 
	EXEC sp_OAMethod @req, 'open', NULL, 'post', @URL, 'false'
	EXEC sp_OAMethod @req, 'setRequestHeader', null, 'Content-Type', 'application/json'
	EXEC sp_OAMethod @req, 'send', null, @body
	EXEC sp_OAGetProperty @req, 'status', @status OUT

	if (@status <> 200) 
	begin
		INSERT INTO @ResponseText (ResponseText) EXEC sp_OAGetProperty @req, 'responseText'
		set @msg  = @URL + ' => Error Code ' +  cast(@status as varchar(10))
			 + CHAR(10) + (select distinct isnull(responsetext,'') from @ResponseText);
		THROW 51000, @msg, 1;  
	end

	INSERT INTO @ResponseText VALUES(@URL + ' <OK>');
	INSERT INTO trestlog(responsetext) select responsetext from @ResponseText;

	EXEC sp_OADestroy @req


END;